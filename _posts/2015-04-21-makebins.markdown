---
layout: post
title:  "RD gotchas"
date:   2015-04-21
categories: econometrics, stata
---

# Problem

I wanted a simple routine to make bins for piecewise linear regression in Stata. Binning routines (e.g. egen's `cut, icodes`) exist, but I wanted something that was easier to specify _and_ let me bin everything below and above certain numbers into the first and last bins, with equal sized bins in between. 

# Solution

Was pretty easy to code, and since it should be useful in a few different projects I'm putting it here:

    program define makebins
        syntax varname, [PREfix(name) min(real 0) max(real 100) step(real 1)]
        local var `varlist'
        
        if mod(`max' - `min',`step') != 0 {
            di as error "Warning: Step size `step' does not divide evenly into range. Top bin will have extend beyond `max'."
        }
        
        * Declare prefix
        if "`prefix'" != "" {
            local p `prefix'
        }
        else {
            local p b_`var'_
        }
        
        local numbins = ceil((`max' - `min') / `step') + 2
        local maxbin = `numbins' - 1

        tempvar binvar
        qui gen int `binvar' = floor((`var' - `min') / `step') + 1
        qui replace `binvar' = 0 if `binvar' < 0
        qui replace `binvar' = `maxbin' if `binvar' > `maxbin'

        * Create labeled bin dummies
        forvalues b = 0/`=`numbins'-1' {
                * Labels for all bins
            local bmin = `b' * `step' + `min' - `step'
            local bmax = `b' * `step' + `min'
            
            * Labels for edge bins
            if (`b' == 0) {
                local bmin "-\infty"
            }
            if (`b' == `maxbin') {
                local bmax "\infty"
            }
            * Create dummy, label it
            capture drop `p'`b'
            quietly gen `p'`b' = `binvar' == `b'
            label var `p'`b' "[`bmin', `bmax')" 
        }
    end